% smp3.sal
%
% Simple Message Passing Model with 3 Nodes
% version 1
%
% Author: Benjamin F Jones <bjones@galois.com>, Galois Inc. (2015)


smp3: CONTEXT =
BEGIN


%=== Nodes and IDs -----------------------------------------------===

% IDs for nodes
IDENTITY:  TYPE     = [0..2];

% Static node IDs
node_A: IDENTITY = 0;  % source
node_B: IDENTITY = 1;  % reciever
node_C: IDENTITY = 2;  % reciever 2

%=== Faults ------------------------------------------------------===

FAULTS: TYPE = ARRAY IDENTITY OF BOOLEAN;


%=== Channels ----------------------------------------------------===

% channel IDs
%   1:  node_A -> node_B
%   2:  node_B -> node_C
CHANNEL: TYPE = [1..2];

linked?(i:IDENTITY, j:IDENTITY): BOOLEAN =
  (i = node_A AND j = node_B) OR (i = node_B AND j = node_C);

chan(i:IDENTITY, j:IDENTITY): CHANNEL =
  IF i = 0 THEN 1 ELSE 2 ENDIF;


%=== Time --------------------------------------------------------===

% Time is modeled by non-negative integers
% -1 is reserved as an invalid time
% -2 is reserved as an atomic "out-of-band" time
TIME: TYPE = [-2..2];
invalid_time: TIME = -1;
atomic_time: TIME  = -2;
msg_delay: TIME    = 1;


%=== Messages and Values -----------------------------------------===

% values recieved by relays and receivers
MESSAGE: TYPE = { m: INTEGER | m >= -1 };
% some named messages
good: MESSAGE    = 0;
missing: MESSAGE = -1;

% these messages are emitted by the faulty nodes
FAULT_MESSAGE: TYPE = { x: MESSAGE | x >= 0 };
uninterp_msg(x: TIME, i: IDENTITY, j: IDENTITY): FAULT_MESSAGE;

is_faulty_msg?(m: MESSAGE): BOOLEAN = m > 0;


%=== Calendar Automata -------------------------------------------===

% Calendar data structure for communication between nodes. The structure
% allows for at most 1 message to be sent from any node to any other
% at a given time.

CALENDAR: TYPE =
  [# msg  : ARRAY CHANNEL of MESSAGE,  % contents of message
     time : ARRAY CHANNEL of TIME      % time for delivery
  #];

empty_calendar: CALENDAR =
  (# msg  := [[c:CHANNEL] missing],
     time := [[c:CHANNEL] invalid_time]
  #);


msg(s: TIME, cal: CALENDAR, f: FAULTS, i:IDENTITY, j:IDENTITY): MESSAGE =
  IF f[i] THEN
    uninterp_msg(s, i, j)  % return uninterpreted message
  ELSE
    cal.msg[chan(i,j)]     % return correct message
  ENDIF;

% Helper functions for getting/setting the calendar
null?(cal: CALENDAR, c: CHANNEL): BOOLEAN =
  cal.msg[c] = missing AND cal.time[c] = invalid_time;

% Is the given time the minimum among all valid times on the calendar?
is_next_time?(cal:CALENDAR, t:TIME): BOOLEAN =
      (FORALL (c:CHANNEL): NOT null?(cal,c) => t <= cal.time[c])
  AND (EXISTS (c:CHANNEL): NOT null?(cal,c) AND t = cal.time[c]);

delivery(cal: CALENDAR, i:IDENTITY, j:IDENTITY): TIME =
  cal.time[chan(i,j)];

% Is there a regular event i -> j yet to occur at the given time?
% Returns FALSE if
%
%     A) the pair i,j has no channel between them OR
%     B) the current time is 'atomic_time'
%
node_event?(cal:CALENDAR, i:IDENTITY, j:IDENTITY, t:TIME): BOOLEAN =
      linked?(i,j)
  AND is_next_time?(cal,t)
  AND NOT null?(cal,chan(i,j))
  AND delivery(cal,i,j) = t
  AND t /= atomic_time;


% Is there an atomic event i -> i (self message) yet to occur?
% Returns FALSE if
%
%     A) there is no self message in the calendar scheduled
%        for t = atomic_time
%     B) current time is not atomic
%
atomic_node_event?(cal:CALENDAR, i:IDENTITY, t:TIME): BOOLEAN =
      is_next_time?(cal, t)
  AND NOT null?(cal,chan(i,i))
  AND delivery(cal,i,i) = t
  AND t = atomic_time;

% is there an event yet to occur at the given time?
event_pending?(cal:CALENDAR, t:TIME): BOOLEAN =
  (EXISTS (c:CHANNEL): cal.msg[c] /= missing AND cal.time[c] = t);

% is there an atomic event pending?
atomic_pending?(cal: CALENDAR): BOOLEAN = event_pending?(cal, atomic_time);

% Am I in atomic mode?
i_am_atomic?(cal: CALENDAR, i: IDENTITY, t: TIME): BOOLEAN =
      EXISTS (j: IDENTITY): linked?(i,j) AND cal.time[chan(j,i)] = atomic_time
  AND t = atomic_time;

% send a message with given delay
msg_send_delta(cal: CALENDAR, i: IDENTITY, j:IDENTITY, m:MESSAGE, t:TIME, d:TIME): CALENDAR =
  cal WITH .msg[chan(i,j)]  := m
      WITH .time[chan(i,j)] := t + d;

% default msg_send function has 'msg_delay' delay
msg_send(cal: CALENDAR, i: IDENTITY, j:IDENTITY, m:MESSAGE, t:TIME): CALENDAR =
  msg_send_delta(cal,i,j,m,t,msg_delay);

% add a 'preemtive' message (or interrupt) to the calendar. This action
% prevents transitions from occuring except in the 'atomic' section of the
% calling module.
msg_send_interrupt(cal: CALENDAR, i: IDENTITY, j:IDENTITY): CALENDAR =
  msg_send_delta(cal,i,j,good,atomic_time,0);

msg_consume(cal: CALENDAR, i: IDENTITY, j:IDENTITY): CALENDAR =
  cal WITH .msg[chan(i,j)]  := missing
      WITH .time[chan(i,j)] := invalid_time;


%===----------------------------------------------------------------===
%       Transition System
%===----------------------------------------------------------------===

%
% The 'clock' module gives the general its message and manages the
% advance of time.
clock: MODULE =
BEGIN
    GLOBAL
      cal: CALENDAR,
      t:   TIME,
      f:   FAULTS
    INPUT
      node_A_done: BOOLEAN,
      node_B_done: BOOLEAN,
      node_C_done: BOOLEAN
INITIALIZATION
    cal = empty_calendar;
    t = 0;
TRANSITION
    [
      advance_time:
        NOT is_next_time?(cal,t) --> t' IN { s:TIME | is_next_time?(cal,s) }
    []
      stutter:
        node_A_done AND node_B_done AND node_C_done -->  % nothing
    ]
END;


node_A: MODULE =
BEGIN
    GLOBAL cal: CALENDAR
    INPUT  t: TIME
    OUTPUT node_A_done: BOOLEAN
INITIALIZATION
    node_A_done = FALSE;
TRANSITION
    [
      send:
      NOT node_A_done AND t = 0 -->
        cal' = msg_send(cal, node_A, node_B, good, t);
        node_A_done' = TRUE;
    ]
END;


node_B: MODULE =
BEGIN
    GLOBAL
        cal: CALENDAR
    INPUT
        t: TIME,
        f: FAULTS
    OUTPUT
        node_B_done: BOOLEAN
INITIALIZATION
    node_B_done  = FALSE;
TRANSITION
    [
      recv_B:
      NOT node_B_done AND node_event?(cal, node_A, node_B, t) -->
        node_B_done'  = TRUE;
        cal' = LET m:MESSAGE = msg(t, cal, f, node_A, node_B) IN
               LET cal1:CALENDAR = msg_send(cal, node_B, node_C, m, t) IN
               msg_consume(cal1, node_A, node_B);
    ]
END;


node_C: MODULE =
BEGIN
    GLOBAL
        cal: CALENDAR
    INPUT
        t: TIME,
        f: FAULTS
    OUTPUT
        node_C_done:  BOOLEAN,
        node_C_latch: MESSAGE
INITIALIZATION
    node_C_done  = FALSE;
    node_C_latch = missing;
TRANSITION
    [
      recv_C:
      NOT node_C_done AND node_event?(cal, node_B, node_C, t) -->
        node_C_done'  = TRUE;
        node_C_latch' = msg(t, cal, f, node_B, node_C);
        cal'          = msg_consume(cal, node_B, node_C);
    ]
END;


%===----------------------------------------------------------------===
%       System
%===----------------------------------------------------------------===

% asyncronous composition is used to better model message passing
system: MODULE =
        clock
     [] node_A
     [] node_B
     [] node_C;


%===----------------------------------------------------------------===
%       Syncronous Observer
%===----------------------------------------------------------------===

observer: MODULE =
BEGIN
  INPUT
    node_A_done: BOOLEAN,
    node_B_done: BOOLEAN,
    node_C_done: BOOLEAN,
    node_C_latch: MESSAGE,
    f: FAULTS
  OUTPUT
    aok: BOOLEAN,
    not_done: BOOLEAN
INITIALIZATION
  aok = TRUE;
  not_done = TRUE
TRANSITION
  [
    no_validity:
      node_C_done AND (NOT node_C_latch=good) -->
        aok' = FALSE
  []
    all_done:
      node_A_done AND node_B_done AND node_C_done -->
        not_done' = FALSE
  []
    ELSE -->
  ]
END;



system_obs : MODULE = system || observer;


%===--------------------------------------------------------------===
%       Verification
%===--------------------------------------------------------------===


%=== Invariants ===

global_time_not_null(t: TIME): BOOLEAN = t /= -1;

monotonic_time(cal: CALENDAR, t: TIME): BOOLEAN =
 FORALL (s: TIME): is_next_time?(cal, s) => s = atomic_time OR s >= t;

missing_cal(cal: CALENDAR): BOOLEAN =
  FORALL (i: CHANNEL): cal.msg[i] = missing <=> cal.time[i] = invalid_time;

unique_atomic(cal: CALENDAR): BOOLEAN =
  FORALL (i,j: CHANNEL): cal.time[i] = -2 AND cal.time[j] = -2 => i = j;

% XXX not general!
node_sets_latch(nbdone: BOOLEAN, nblatch: MESSAGE): BOOLEAN =
  nbdone => nblatch /= missing;


%===   Lemmas   ===

% proven: improved from k=3 to k=1 by splitting node_event into the two
% versions
%
% sal-inf-bmc -v 1 --delta-path -i -ice -d 1 smp3.sal lemma_conj
lemma_conj: LEMMA system_obs |-
  G(    monotonic_time(cal, t)
    AND global_time_not_null(t)
    AND missing_cal(cal)
    AND unique_atomic(cal)
    AND node_sets_latch(node_C_done, node_C_latch)
   );

% proved: inductive
% sal-inf-bmc -v 1 --delta-path -i -ice -d 1 smp3.sal afault
afault: LEMMA system_obs |-
  G(is_faulty_msg?(cal.msg[chan(node_A, node_B)]) => f[node_A]);

% proved: inductive
% sal-inf-bmc -v 1 --delta-path -i -ice -d 1 -l afault smp3.sal abfault
abfault: LEMMA system_obs |-
  G(is_faulty_msg?(cal.msg[chan(node_B, node_C)]) =>
      f[node_A] OR f[node_B]);

% XXX not general!
% proved: 3-inductive
%
% sal-inf-bmc -v 1 --delta-path -i -ice -d 1 -l lemma_conj -l afault -l abfault smp3.sal fault_propagation
fault_propagation: LEMMA system_obs |-
  G(is_faulty_msg?(node_C_latch) => f[node_A] OR f[node_B]);


%===  Main Theorems  ===

% Maximum fault assumption
mfa(f: FAULTS): BOOLEAN = FORALL (i: IDENTITY): NOT f[i];

% proved: inductive!
% sal-inf-bmc -v 1 --delta-path -i -ice -d 1 -l lemma_conj -l fault_propagation smp3.sal va
vaa: THEOREM system_obs |- G(mfa(f) => aok);


END
