% smp.sal
%
% Simple Message Passing Model
% version 1
%
% Author: Benjamin F Jones <bjones@galois.com>, Galois Inc. (2015)
%
% The following is a *very simple* model of two-node communication using
% calendar automata, a fault model, and verification using a synchronous
% observer. The system is:
%
%     A  -->  B
%
% where A send a single message to B and B, upon receipt, stores the message
% in a 'latch'.
%
% The main theorem proved is that if A is not faulty, then B does receive the
% message and the value sent by A is the same value stored in the latch.
%

smp: CONTEXT =
BEGIN


%=== Nodes and IDs -----------------------------------------------===

% IDs for nodes
IDENTITY:  TYPE     = [0..1];

% Static node IDs
node_A: IDENTITY = 0;  % source
node_B: IDENTITY = 1;  % reciever


%=== Faults ------------------------------------------------------===

FAULTS: TYPE = ARRAY IDENTITY OF BOOLEAN;


%=== Channels ----------------------------------------------------===

% channel IDs
%   1:  node_A -> node_B
CHANNEL: TYPE = [1..1];

linked?(i:IDENTITY, j:IDENTITY): BOOLEAN = i = node_A AND j = node_B;

chan(i:IDENTITY, j:IDENTITY): CHANNEL = 1;  % there is only Zuul


%=== Time --------------------------------------------------------===

% Time is modeled by non-negative integers
% -1 is reserved as an invalid time
% -2 is reserved as an atomic "out-of-band" time
TIME: TYPE = [-2..1];
invalid_time: TIME = -1;
atomic_time: TIME = -2;
msg_delay: TIME = 1;


%=== Messages and Values -----------------------------------------===

% values recieved by relays and receivers
%
% MESSAGES is modeled as an uninterpreted type to support arbitrary
% faulty values

%MESSAGE: TYPE = { m: INTEGER | m >= -1 AND m <= 3 };
MESSAGE: TYPE = { m: INTEGER | m >= -1 };
% some named messages
good_msg: MESSAGE = 0;
missing:  MESSAGE = -1;

% these messages are emitted by the faulty nodes
FAULT_MESSAGE: TYPE = { x: MESSAGE | x >= 0 };
uninterp_msg(x: TIME, i: IDENTITY, j: IDENTITY): FAULT_MESSAGE;

is_faulty_msg?(m: MESSAGE): BOOLEAN = m > 0;


%=== Calendar Automata -------------------------------------------===

% Calendar data structure for communication between nodes. The structure
% allows for at most 1 message to be sent from any node to any other
% at a given time.

CALENDAR: TYPE =
  [# msg  : ARRAY CHANNEL of MESSAGE,  % contents of message
     time : ARRAY CHANNEL of TIME      % time for delivery
  #];

empty_calendar: CALENDAR =
  (# msg  := [[c:CHANNEL] missing],
     time := [[c:CHANNEL] invalid_time]
  #);

% Calendar API

msg(s: TIME, cal: CALENDAR, f: FAULTS, i:IDENTITY, j:IDENTITY): MESSAGE =
  IF f[i] THEN
    uninterp_msg(s, i, j)  % return a faulty message
  ELSE
    cal.msg[chan(i,j)]     % return correct message
  ENDIF;

% Helper functions for getting/setting the calendar
null?(cal: CALENDAR, c: CHANNEL): BOOLEAN =
  cal.msg[c] = missing AND cal.time[c] = invalid_time;

% Is the given time the minimum among all valid times on the calendar?
is_next_time?(cal:CALENDAR, t:TIME): BOOLEAN =
      (FORALL (c:CHANNEL): NOT null?(cal,c) => t <= cal.time[c])
  AND (EXISTS (c:CHANNEL): NOT null?(cal,c) AND t = cal.time[c]);

delivery(cal: CALENDAR, i:IDENTITY, j:IDENTITY): TIME =
  cal.time[chan(i,j)];

% Is there an event i -> j yet to occur at the given time?
% Returns FALSE if
%
%     A) the pair i,j has no channel between them OR
%     B) there is a pending atomic event.
%
node_event?(cal:CALENDAR, i:IDENTITY, j:IDENTITY, t:TIME): BOOLEAN =
      linked?(i,j)
  AND is_next_time?(cal,t)
  AND NOT null?(cal,chan(i,j))
  AND delivery(cal,i,j) = t;

% is there an event yet to occur at the given time?
event_pending?(cal:CALENDAR, t:TIME): BOOLEAN =
  (EXISTS (c:CHANNEL): cal.msg[c] /= missing AND cal.time[c] = t);

% is there an atomic event pending?
atomic_pending?(cal: CALENDAR): BOOLEAN = event_pending?(cal, atomic_time);

% Am I in atomic mode?
i_am_atomic?(cal: CALENDAR, i: IDENTITY, t: TIME): BOOLEAN =
      EXISTS (j: IDENTITY): linked?(i,j) AND cal.time[chan(j,i)] = atomic_time
  AND t = atomic_time;

% send a message with given delay
msg_send_delta(cal: CALENDAR, i: IDENTITY, j:IDENTITY, m:MESSAGE, t:TIME, d:TIME): CALENDAR =
  cal WITH .msg[chan(i,j)]  := m
      WITH .time[chan(i,j)] := t + d;

% default msg_send function has 'msg_delay' delay
msg_send(cal: CALENDAR, i: IDENTITY, j:IDENTITY, m:MESSAGE, t:TIME): CALENDAR =
  msg_send_delta(cal,i,j,m,t,msg_delay);

% add a 'preemtive' message (or interrupt) to the calendar. This action
% prevents transitions from occuring except in the 'atomic' section of the
% calling module.
msg_send_interrupt(cal: CALENDAR, i: IDENTITY, j:IDENTITY): CALENDAR =
  msg_send_delta(cal,i,j,good_msg,atomic_time,0);

msg_consume(cal: CALENDAR, i: IDENTITY, j:IDENTITY): CALENDAR =
  cal WITH .msg[chan(i,j)]  := missing
      WITH .time[chan(i,j)] := invalid_time;


%===----------------------------------------------------------------===
%       Transition System
%===----------------------------------------------------------------===

%
% The 'clock' module gives the general its message and manages the
% advance of time.
clock: MODULE =
BEGIN
    GLOBAL
      cal: CALENDAR,
      t:   TIME,
      % Non-deterministically assign which nodes are faulty
      faulty?: FAULTS
    INPUT
      node_A_done: BOOLEAN,
      node_B_done: BOOLEAN
INITIALIZATION
    cal = empty_calendar;
    t = 0;
TRANSITION
    [
      advance_time:
        NOT is_next_time?(cal,t) --> t' IN { s:TIME | is_next_time?(cal,s) }
    []
      stutter:
        node_A_done AND node_B_done -->  % nothing
    ]
END;


node_A: MODULE =
BEGIN
    GLOBAL cal: CALENDAR
    INPUT  t: TIME
    OUTPUT node_A_done: BOOLEAN
INITIALIZATION
    node_A_done = FALSE;
TRANSITION
    [
      send:
      NOT node_A_done AND t = 0 -->
        cal' = msg_send(cal, node_A, node_B, good_msg, t);
        node_A_done' = TRUE;
    ]
END;


node_B: MODULE =
BEGIN
    GLOBAL
        cal: CALENDAR
    INPUT
        t: TIME,
        faulty?: FAULTS
    OUTPUT
        node_B_done: BOOLEAN,
        node_B_latch: MESSAGE
INITIALIZATION
    node_B_done  = FALSE;
    node_B_latch = missing;
TRANSITION
    [
      recv:
      NOT node_B_done AND node_event?(cal, node_A, node_B, t) -->
        node_B_done'  = TRUE;
        node_B_latch' = msg(t, cal, faulty?, node_A, node_B);
        cal'          = msg_consume(cal, node_A, node_B);
    ]
END;


%===----------------------------------------------------------------===
%       System
%===----------------------------------------------------------------===

% asyncronous composition is used to better model message passing
system: MODULE =
        clock
     [] node_A
     [] node_B;


%===----------------------------------------------------------------===
%       Syncronous Observer
%===----------------------------------------------------------------===

observer: MODULE =
BEGIN
  INPUT
    node_A_done: BOOLEAN,
    node_B_done: BOOLEAN,
    node_B_latch: MESSAGE
  OUTPUT
    aok: BOOLEAN,
    not_done: BOOLEAN
INITIALIZATION
  aok = TRUE;
  not_done = TRUE
TRANSITION
  [
    no_validity:
      node_B_done AND (node_B_latch /= good_msg) -->
        aok' = FALSE
  []
    all_done:
      node_A_done AND node_B_done -->
        not_done' = FALSE
  []
    ELSE -->
  ]
END;


% transition system + synchronous observer
system_obs : MODULE = system || observer;


%===--------------------------------------------------------------===
%       Verification
%===--------------------------------------------------------------===


%=== Invariants ===

%% Predicates

global_time_not_null(t: TIME): BOOLEAN = t /= -1;

monotonic_time(cal: CALENDAR, t: TIME): BOOLEAN =
 FORALL (s: TIME): is_next_time?(cal, s) => s = atomic_time OR s >= t;

missing_cal(cal: CALENDAR): BOOLEAN =
  FORALL (i: CHANNEL): cal.msg[i] = missing <=> cal.time[i] = invalid_time;

unique_atomic(cal: CALENDAR): BOOLEAN =
  FORALL (i,j: CHANNEL): cal.time[i] = -2 AND cal.time[j] = -2 => i = j;

% XXX not general!
node_B_sets_latch(nbdone: BOOLEAN, nblatch: MESSAGE): BOOLEAN =
  nbdone => nblatch /= missing;


%% LEMMAs

% proved:
% sal-inf-bmc -v 1 -i -ice -d 1 -l lemma_conj smp.sal afault
afault: LEMMA system_obs |-
  G(is_faulty_msg?(cal.msg[chan(node_A, node_B)]) => faulty?[node_A]);

% XXX not very general! it depends on knowing how messages are sent through
% the system and saved to specific node-local variables
%
% proved:
% sal-inf-bmc -v 1 -i -ice -d 1 -l lemma_conj -l afault smp fault_propagation
fault_propagation: LEMMA system_obs |-
  G(   node_B_latch = good_msg
    OR node_B_latch = missing
    OR faulty?[node_A]
   );

% proved: inductive
% sal-inf-bmc -v 1 --delta-path -i -ice -d 1 smp.sal lemma_conj
lemma_conj: LEMMA system_obs |-
  G(monotonic_time(cal, t)
     AND global_time_not_null(t)
     AND missing_cal(cal)
     AND unique_atomic(cal)
     AND node_B_sets_latch(node_B_done, node_B_latch)
   );


%===  Main Theorems  ===

% Maximum fault assumption
mfa(f: FAULTS): BOOLEAN = FORALL (i: IDENTITY): NOT f[i];

% Validity and Agreement
%
% proved!
% sal-inf-bmc -v 1 -i -ice -d 1 -l lemma_conj -l fault_propagation smp.sal vaa
vaa: THEOREM system_obs |- G(mfa(faulty?) => aok);

END
