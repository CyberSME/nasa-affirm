semantics: THEORY
BEGIN

% Environmental signals: clocks and events. The environment can be thought of as
% a "global" process that can send messages to every process.
Env     : Type+

% Channels between processes
Chan    : Type+

% Messages send over channels or from the environment
Msg     : Type+
nullMsg : Msg
clkMsg  : nonempty_pred[Msg]

% Processes
Proc    : Type+

% A process' state
State       : Type+
stateInit   : State

% Full system state generated from each process' state.
GlobalState : Type = [Proc -> State]
globalInit  : GlobalState

% An event is unique in the system. Events are points in time (e.g., calendars)
% at which one or more system events occurs.
Time   : Type = { r:real | r >= 0 }
Event  : Type = nat

c     : VAR Chan
e     : VAR Env
v     : VAR Event
tx,rx : VAR Proc
s     : VAR State
t     : VAR Time
m     : VAR Msg

% --------------------------------------
% Implementation-defined (undefined functions)
% --------------------------------------

% Map from an event to real-time.
fromEvent(v) : Time

% Environmental message received by rx at time t. Either a periodic event or a
% signal.
envMsg(t, rx, e) : Msg

% Message sent by tx into channel c at time t.
outMsg(t, tx, c) : Msg

% Message pulled out of channel c by rx at time t.
inMsg(t, rx, c) : Msg

% The inbound channels of rx.
inChan(rx) : pred[Chan]
% Helper
InChan(rx) : Type = { c | inChan(rx)(c) }

% State update function for a process.
update(rx, s, m) : State

% (Nondeterministically) fold over the rx's channels, updating the state based
% on each inbound message, to create a new state.
foldStates(s, rx, (f:[InChan(rx), State -> State])): State

% --------------------------------------
% Defined
% --------------------------------------

% A clock event is tied to a channel.
hasClock(t, rx, c) : bool =
  EXISTS e :
    clkMsg(envMsg(t, rx, e))

noClock(rx, c) : bool =
  FORALL t, e :
    NOT clkMsg(envMsg(t, rx, e))

% pull semantics: rx gets message m at time t on channel c if some tx0 sent m
% earlier and no tx1 sent m1 /= m in the interim.
pull(t, rx, c) : bool =
    hasClock(t, rx, c)
  AND
    EXISTS (tx0:Proc), (t0:Time):
        outMsg(t0, tx0, c) = inMsg(t, rx, c)
      AND t0 < t
      AND
        FORALL (tx1:Proc), (t1:Time) :
        EXISTS (m1:Msg) :
               outMsg(t1, tx1, c) = m1
             AND t1 < t AND t0 < t1
           IMPLIES inMsg(t, rx, c) = m1

% push semantics: rx gets message m at time t on channel c if tx0 sent m at t
% and no tx1 sent m1 / m at t, too. (For push semantics, we assume that
% logically, there is no delay in sending and receiving).
push(t, rx, c) : bool =
    noClock(rx, c)
  AND
    EXISTS (tx0:Proc):
        outMsg(t, tx0, c) = inMsg(t, rx, c)
      AND
        FORALL (tx1:Proc) :
        EXISTS (m1:Msg) :
               outMsg(t, tx1, c) = m1
           IMPLIES inMsg(t, rx, c) = m1

% Transitions in the system progress according to rounds, where each round is a
% global time at which one or more events occurs. rnd computes the state update
% for an individual process upon receiving a message (if no messages is
% received---i.e., m = nullMsg---then the state is not updated.  Note that we
% don't need to model explicitly outMsg or receiving environmental messages;
% they are captured by our axioms.
rnd(v, rx) : RECURSIVE State =
    IF v = 0 THEN stateInit
    ELSE foldStates(rnd(v-1, rx), rx, f)
      WHERE
      f = LAMBDA (c0 : InChan(rx)), s :
            update(rx, s, inMsg(t, rx, c0))
    ENDIF
    WHERE t = fromEvent(v)
  MEASURE v

% Global run.
run(v) : GlobalState =
  LAMBDA rx : rnd(v, rx)

% --------------------------------------
% AXIOMS
% --------------------------------------

fromEvent_AX : AXIOM
  FORALL (v0 : Event, v1 : Event) :
	  v0 <= v1 IMPLIES fromEvent(v0) <= fromEvent(v1)

fold_AX : AXIOM
  foldStates(s, rx, LAMBDA (c0 : InChan(rx)), s : s) = s

% The state does not update upon receiving a null message.
stutterState_AX : AXIOM
  update(rx, s, nullMsg) = s

% Messages are only received if either a push or pull event occurs.
inMsg_AX : AXIOM
    pull(t, rx, c)
  OR
    push(t, rx, c)
  OR
    inMsg(t, rx, c) = nullMsg

% A process only receives events at a calendar event.
completeEvents_AX : AXIOM
  FORALL (t : { t |   t > fromEvent(v)
                  AND t < fromEvent(v+1)
              }) :
    inMsg(fromEvent(v), rx, c) = nullMsg

stutterState_LEM : LEMMA
          (FORALL c : (nullMsg = inMsg(fromEvent(v), rx, c)))
  IMPLIES (v = 0 OR rnd(v, rx) = rnd(v-1, rx))

END semantics

semantics_interp: THEORY
BEGIN

IMPORTING semantics

% 2 processes and one channel that each process can read. All messages are clock
% potential messages. Each process sends its state+1 on the channel. Every
% process gets a clock signal on the naturals.

Proc : TYPE = {n:nat | n >= 0 AND n <= 1}
Chan : TYPE = {n:nat | n = 0 }
Msg  : Type = nat

IMPORTING semantics
  {{ Env        := nat
   , Chan       := Chan
   , Msg        := Msg
   , nullMsg    := 0
   , clkMsg     := LAMBDA (m:nat) : TRUE
   , Proc       := Proc
   , State      := nat
   , stateInit  := 0
   , globalInit := LAMBDA (p:nat) : 0
   , fromEvent  := LAMBDA (v:nat) : v
   , envMsg     := LAMBDA (t:Time, rx:Proc, e:nat) : IF t = floor(t) THEN floor(t) ELSE 0 ENDIF
   , outMsg     := LAMBDA (t:Time, tx:Proc, c:Chan) : 0
   , inMsg      := LAMBDA (t:Time, rx:Proc, c:Chan) : 0
   , inChan     := LAMBDA (rx:Proc) : LAMBDA (c:Chan) : rx = 1
   , update     := LAMBDA (rx:Proc, s:nat, m:nat) : s
   , foldStates := LAMBDA (s:nat, rx:Proc, f: [[{c:Chan | IF rx = 0 THEN FALSE ELSE TRUE ENDIF}], nat -> nat]) : s
   }}


END semantics_interp
